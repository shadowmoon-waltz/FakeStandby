apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

boolean signConfigExists = project.hasProperty("keystoreFile") && project.hasProperty("keystorePassword") && project.hasProperty("keystoreAlias") && project.hasProperty("keystoreAliasPassword")

android {
    if (signConfigExists) {
        signingConfigs {
            release {
                storeFile file(project.property("keystoreFile"))
                storePassword project.property("keystorePassword")
                keyAlias project.property("keystoreAlias")
                keyPassword project.property("keystoreAliasPassword")
            }
        }
    }

    compileSdkVersion 30
    defaultConfig {
        applicationId "android.jonas.fakestandby"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 7
        versionName '1.3.1'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            lintOptions {
                checkReleaseBuilds false
                abortOnError false
            }
        }
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix = '-debug'
        }
    }
    flavorDimensions 'ads'
    productFlavors {
        ad {
            dimension = 'ads'
        }
        adfree {
            dimension = 'ads'
        }
    }
}

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //implementation 'com.android.support:appcompat-v7:28.0.0'
    //implementation "com.android.support:support-compat:28.0.0"

    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    //implementation 'com.github.AppIntro:AppIntro:6.0.0'

    adImplementation 'com.google.firebase:firebase-analytics:17.6.0'
    adImplementation 'com.google.android.gms:play-services-ads:19.4.0'

    testImplementation 'junit:junit:4.13'

    androidTestImplementation 'tools.fastlane:screengrab:1.2.0'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

if (!getGradle().getStartParameter().getTaskRequests().toString().contains("Adfree") && !getGradle().getStartParameter().getTaskRequests().toString().contains("adfree")){
    apply plugin: 'com.google.gms.google-services'
}
